plugins {
	id 'org.jetbrains.kotlin.jvm' version '1.9.25'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.9.25'
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.google.protobuf' version '0.9.4'
	id 'org.jetbrains.kotlin.plugin.jpa' version '1.9.25'
	id 'org.jetbrains.kotlin.kapt' version '1.9.22'
}

group = 'io.madeformaid'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
	maven {
		name = "GitHubPackages"
		url = uri("https://maven.pkg.github.com/tmdghks2515/made-for-maid.shared-lib")
		credentials {
			username = project.findProperty("gpr.user")
			password = project.findProperty("gpr.key")
		}
	}
	maven {
		name = "GitHubPackages"
		url = uri("https://maven.pkg.github.com/tmdghks2515/made-for-maid.webmvc-lib")
		credentials {
			username = project.findProperty("gpr.user")
			password = project.findProperty("gpr.key")
		}
	}
	maven {
		name = "GitHubPackages"
		url = uri("https://maven.pkg.github.com/tmdghks2515/made-for-maid.proto-files")
		credentials {
			username = project.findProperty("gpr.user")
			password = project.findProperty("gpr.key")
		}
	}
}

ext {
	set('springGrpcVersion', "0.6.0")
	set('springCloudVersion', "2024.0.1")
}

configurations {
	protoFiles {
		canBeResolved = true
		canBeConsumed = false
	}
}

dependencies {
	implementation 'io.madeformaid:shared-lib:0.0.1-SNAPSHOT'
	implementation 'io.madeformaid:webmvc-lib:0.0.1-SNAPSHOT'
	protoFiles  'io.madeformaid:proto-files:0.0.1-SNAPSHOT'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'io.grpc:grpc-services'
	implementation "io.grpc:grpc-kotlin-stub:1.4.1"
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'org.springframework.grpc:spring-grpc-server-web-spring-boot-starter'
	implementation 'org.springframework.cloud:spring-cloud-starter-config'
	implementation 'com.auth0:java-jwt:4.4.0'
	implementation("com.querydsl:querydsl-jpa:5.0.0:jakarta")
	kapt("com.querydsl:querydsl-apt:5.0.0:jakarta")
	kapt("jakarta.annotation:jakarta.annotation-api")
	kapt("jakarta.persistence:jakarta.persistence-api")
	runtimeOnly 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
	testImplementation 'org.springframework.grpc:spring-grpc-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.grpc:spring-grpc-dependencies:${springGrpcVersion}"
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

kotlin {
	jvmToolchain {
		languageVersion.set(JavaLanguageVersion.of(21))
	}
}

/* grpc proto 설정부 - START */
// 📦 proto JAR 추출 경로 설정
def extractedProtoDir = layout.buildDirectory.dir("proto-extracted").get().asFile

// 📥 proto JAR에서 .proto만 추출
tasks.register('extractProtoFiles', Copy) {
	from { configurations.protoFiles.collect { zipTree(it) } }
	include '**/*.proto'
	into extractedProtoDir
}

// 📁 sourceSets에 proto 설정 (generateProto 전에 등록돼야 함)
sourceSets.main.proto.srcDir extractedProtoDir

// 🧩 의존성 연결
tasks.named('generateProto') { dependsOn 'extractProtoFiles' }
tasks.named('processResources') { dependsOn 'extractProtoFiles' }

// ⚙️ protobuf stub 생성 설정
protobuf {
	protoc { artifact = "com.google.protobuf:protoc:3.25.2" }
	plugins {
		grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.62.2" }
		grpckt { artifact = "io.grpc:protoc-gen-grpc-kotlin:1.4.1:jdk8@jar" }
	}
	generateProtoTasks {
		all().each { task ->
			task.builtins { java {}; kotlin {} }
			task.plugins { grpc {}; grpckt {} }
		}
	}
}
/* grpc proto 설정부 - END */

allOpen {
	annotation 'jakarta.persistence.Entity'
	annotation 'jakarta.persistence.MappedSuperclass'
	annotation 'jakarta.persistence.Embeddable'
}

// QueryDSL 설정부 - START
def generated = layout.buildDirectory.dir("generated/source/kapt/main").get().asFile

// clean 시 QClass 삭제
tasks.named("clean") {
	doLast {
		delete generated
	}
}

kapt {
	generateStubs = true
}

// Gradle 9.0 대응 — kapt → generateProto 의존성 선언
afterEvaluate {
	tasks.named("kaptGenerateStubsKotlin") {
		dependsOn(tasks.named("generateProto"))
	}
}
// QueryDSL 설정부 - END

// Kotlin sourceSet에 QClass 디렉토리 및 grpc 포함 (Gradle 9 호환)
sourceSets {
	main {
		kotlin {
			srcDirs += [
					"$buildDir/generated/source/proto/main/grpc",
					"$buildDir/generated/source/proto/main/grpckt", // Kotlin gRPC stub
					generated
			]
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
